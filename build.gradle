apply plugin: 'groovy'

repositories {
    jcenter()
    maven {
        url "http://smartbearsoftware.com/repository/maven2"
    }
}

ext {
    soapuiVersion = '5.2.1'
    soapuiImportedArtifacts = [
            [artifact: 'groovy-all', defaultVersion: '2.1.7'],
            [artifact: 'log4j', defaultVersion: '1.2.14']
    ]
}

dependencies {
    runtime("com.smartbear.soapui:soapui:$soapuiVersion") {
        transitive = false
    }
}

/**
 * Parses POM to extract Groovy and Log4j versions without needing to download all the other SoapUI dependencies
 */
afterEvaluate {

    // Honouring offline mode
    if (project.gradle.startParameter.isOffline()) {
        logger.lifecycle "Offline mode detected: disabling resolution of SoapUI dependencies"
        soapuiImportedArtifacts.each {
            logger.lifecycle("Using default: ${it.artifact}: ${it.defaultVersion}")
        }

    } else {
        def url = "${repositories['maven'].url}/com/smartbear/soapui/soapui/$soapuiVersion/soapui-${soapuiVersion}.pom"

        def pom = new XmlSlurper().parse(url.toURL().newReader())
        pom.dependencies.dependency.each { d ->
            if (d.artifactId in soapuiImportedArtifacts*.artifact) {
                logger.info "Adding SoapUI dependency '${d.groupId}:${d.artifactId}:${d.version}'"
                project.dependencies {
                    compile "${d.groupId}:${d.artifactId}:${d.version}"
                }
            }
        }
    }

}
